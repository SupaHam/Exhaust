buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'lc.vq'
    version = '1.0.1-SNAPSHOT'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    defaultTasks 'build', 'publishToMavenLocal'

    jar {
        classifier = 'original'
    }

    shadowJar {
        classifier = ''
    }

    build.dependsOn(shadowJar)

    processResources {
        filter ReplaceTokens, tokens: [
                "project.version": project.version
        ]
    }


    publishing {
        if(project.hasProperty("maven.authUser")) {
            repositories {
                maven {
                    if (project.version.endsWith('-SNAPSHOT')) {
                        url 'http://repo.ellune.net/content/repositories/snapshots/'
                    } else {
                        url 'http://repo.ellune.net/content/repositories/releases/'
                    }
                    credentials {
                        username project.getProperty("maven.authUser")
                        password project.getProperty("maven.authPass")
                    }
                }
            }
        }

        publications {
            shadow(MavenPublication) {
                from components.java
                artifact shadowJar
            }
        }
    }
}

subprojects {
    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            name 'ellune-repo'
            url 'http://repo.ellune.net/content/groups/public/'
        }

        maven {
            name 'sk89q-repo'
            url 'http://maven.sk89q.com/repo/'
        }
    }

    dependencies {
        compile 'com.sk89q.intake:intake:4.0-SNAPSHOT'
    }
}
